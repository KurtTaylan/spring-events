buildscript {
    ext {
        springBootVersion = '1.3.0.M3'
    }
    repositories {
        mavenCentral()
        maven { url "http://repo.spring.io/snapshot" }
        maven { url "http://repo.spring.io/milestone" }

    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

plugins {
    id "io.spring.dependency-management" version "0.4.1.RELEASE"
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'spring-boot' 

jar {
    baseName = 'spring-events'
    version = '1.0.0-SNAPSHOT'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

ext.h2Version             = "1.4.187"
ext.lombokVersion         = "1.16.4"
ext.springVersion         = "4.2.0.RC1"
ext.springSecurityVersion = "4.0.1.RELEASE"
ext.wiserVersion          = "3.1.7"
ext.assertJVersion        = '3.1.0'

repositories {
    mavenCentral()
    maven { url "http://repo.spring.io/snapshot" }
    maven { url "http://repo.spring.io/milestone" }
}

dependencies {
    compile("org.springframework.boot:spring-boot-starter-actuator")
    compile("org.springframework.boot:spring-boot-starter-aop")
    compile("org.springframework.boot:spring-boot-starter-data-jpa")
    compile("org.springframework.boot:spring-boot-starter-jdbc")
    compile("org.springframework.boot:spring-boot-starter-security")
    compile("org.springframework.boot:spring-boot-starter-thymeleaf")
    compile("org.springframework.boot:spring-boot-starter-web")

    compile("org.projectlombok:lombok:${lombokVersion}")
    compile("com.h2database:h2:${h2Version}")

    testCompile("org.springframework.boot:spring-boot-starter-test")
    testCompile("org.springframework.security:spring-security-test:${springSecurityVersion}")
    testCompile("org.subethamail:subethasmtp:${wiserVersion}")
	// testCompile("de.saly:javamail-mock2-fullmock:0.5-beta4")
    testCompile("org.assertj:assertj-core:${assertJVersion}")
}

eclipse {
    classpath {
         containers.remove('org.eclipse.jdt.launching.JRE_CONTAINER')
         containers 'org.eclipse.jdt.launching.JRE_CONTAINER/org.eclipse.jdt.internal.debug.ui.launcher.StandardVMType/JavaSE-1.8'
    }
}

configurations.all {
	resolutionStrategy.eachDependency { DependencyResolveDetails details ->
		if (details.requested.group == "org.springframework") {
			details.useVersion springVersion
		}
		if (details.requested.group == "org.springframework.security") {
			details.useVersion springSecurityVersion
		}
	}
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.4'
}
